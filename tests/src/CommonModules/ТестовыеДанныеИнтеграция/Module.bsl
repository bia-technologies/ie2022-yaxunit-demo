#Область СлужебныйПрограммныйИнтерфейс

Функция СоздатьНастройкуИнтеграции(ПометкаУдаления = Ложь) Экспорт
	
	Объект = Справочники.НастройкиИнтеграции.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(Объект, ОписаниеНастройкиИнтеграции());
	Объект.ПометкаУдаления = ПометкаУдаления;
	Объект.Записать();
	
	Возврат Объект;
	
КонецФункции

Функция ОписаниеНастройкиИнтеграции() Экспорт
	
	Описание = Новый Структура();
	Описание.Вставить("Адрес", ЮТест.Данные().СлучайнаяСтрока());
	Описание.Вставить("Пользователь", ЮТест.Данные().СлучайнаяСтрока());
	Описание.Вставить("Пароль", ЮТест.Данные().СлучайнаяСтрока());
	Описание.Вставить("Организация", ЮТест.Данные().СоздатьЭлемент(Справочники.Организации));
	Описание.Вставить("Склад", ЮТест.Данные().СоздатьЭлемент(Справочники.Склады));
	Описание.Вставить("Валюта", ЮТест.Данные().СоздатьЭлемент(Справочники.Валюты));
	Описание.Вставить("ВидЦен", ЮТест.Данные().СоздатьЭлемент(Справочники.ВидыЦен));
	
	Возврат Описание;
	
КонецФункции

Функция ТелоОтвета() Экспорт
	
	Возврат Новый Структура("Данные", Новый Массив());
	
КонецФункции

Функция Ответ(Успешно = Истина, КодСостояния = 200, Знач Тело = Неопределено) Экспорт
	
	Если Тело = Неопределено Тогда
		Тело = ТелоОтвета();
	КонецЕсли;
	
	Возврат Новый Структура("Успешно, Ошибки, КодСостояния, Тело", Успешно, Новый Массив(), КодСостояния, Тело);
	
КонецФункции

Функция ЗаписьДанных(Знач Идентификатор = Неопределено,
					 Знач Покупатель = Неопределено,
					 Знач Товар = Неопределено,
					 Знач Количество = Неопределено,
					 Знач Сумма = Неопределено) Экспорт

	Если Идентификатор = Неопределено Тогда
		Идентификатор = ЮТест.Данные().СлучайнаяСтрока();
	КонецЕсли;
	
	Если Покупатель = Неопределено Тогда
		Покупатель = ЮТест.Данные().СлучайнаяСтрока();
		ЮТест.Данные().СоздатьЭлемент(Справочники.Контрагенты, Покупатель);
	КонецЕсли;
	
	Если Товар = Неопределено Тогда
		Товар = ЮТест.Данные().СлучайнаяСтрока();
		Товар(Товар);
	КонецЕсли;
	
	Если Количество = Неопределено Тогда
		Количество = ЮТест.Данные().СлучайноеПоложительноеЧисло(9999);
	КонецЕсли;
	
	Если Сумма = Неопределено Тогда
		Цена = ЮТест.Данные().СлучайноеПоложительноеЧисло(999);
		Сумма = Цена * Количество;
	КонецЕсли;
	
	Шапка = Новый Структура("Идентификатор, Покупатель, Дата, Товары", Идентификатор, Покупатель, "2022-10-06");
	
	ЗаписьОТоваре = Новый Структура("Наименование, Количество, Сумма", Товар, Количество, Сумма);
	
	Шапка.Товары = Новый Массив();
	Шапка.Товары.Добавить(ЗаписьОТоваре);
	
	Возврат Шапка;
	
КонецФункции

Функция Товар(Наименование = Неопределено)
	
	Возврат ЮТест.Данные()
		.СоздатьЭлемент(Справочники.Товары, Наименование, Новый Структура("Вид", Перечисления.ВидыТоваров.Товар));
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Код процедур и функций

#КонецОбласти